# Return the current date in ISO format (without time-zone)
# or the specified date/time
#
:global ISODateTime do={

        :local date
        :local time
	:local dtISO
        :if ([:len "$1"] < 11) do={
                :set date [/system clock get date ];
                :set time [/system clock get time ];
        } else={
		# "mmm/dd/yyyy hh:mm:ss"
                :local p1 [:find "$1 " " "];
                :set date [:pick "$1" 0 $p1];
                :set time [:pick "$1" ($p1+1) ($p1 + 9)];
        }

	# mar/13/2017
	:local day [:pick $date 4 6 ];
	:local month [:pick $date 0 3 ];
	:local year [:pick $date 7 11 ];
	#
	:local months [:toarray "jan,feb,mar,apr,may,jun,jul,aug,sep,oct,nov,dec"];
	:local m [:pick ( ( [:find $months $month -1 ] + 101) . "") 1 3]
	#

	if ([:len "$time"] > 0) do={
		:set dtISO  ($year . "-" . $m . "-" . $day . "T" . $time);
	} else={
		:set dtISO  ($year . "-" . $m . "-" . $day );
	}

	:return $dtISO;

};
