
# Ping test and return if success
# $1 - ping host 
# $2 - total ping counter (if not success will try this number of pings)
# $3 - split ping counter (each ping command will use this number must be at least 2)
# return 0 - no response 
#        n - number of valid ping responses from the last ping command
#
# eg. $PingSplit 8.8.8.8 300 10
#         will try 10 pings each time until 300 if 8.8.8.8 not responding 

:global PingSplit do={
        :local host $1
        :local count $2
        :local splitCount $3
        
        :while ($count > 0) do={
             :set count ($count - $splitCount)
             :local cnt 0
             :local result [/ping address=$host count=$splitCount as-value]
             :foreach p in $result do={
                 #    :put ("$p")
                 if ($p->"ttl" > 0) do={
                     :set cnt ($cnt + 1)
                 }
             }
             :if ( $cnt > 0 ) do={
                 :return $cnt
             }
           
        }
        :return 0
}
